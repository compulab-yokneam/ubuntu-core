setenv tty "ttymxc0,115200"
setenv dev 0
setenv rdaddr 0x82000000
setenv loadaddr 0x80800000
setenv fdtaddr 0x83000000
setenv envaddr 0x83000000

if itest.s "x" == "x${core_rev}" ; then
 core_rev=3306
fi
if itest.s "x" == "x${kernel_rev}" ; then
 kernel_rev=x1
fi

if itest.s "xusb" == "x${boot_media}"
then
 boot_media_init="usb start"
 boot_media=usb
else
 boot_media_init="mmc dev ${dev}"
 boot_media=mmc
fi

setenv ramimg cl-som-imx7-kernel_${kernel_rev}.snap/initrd.img
setenv fdtfile cl-som-imx7-kernel_${kernel_rev}.snap/dtbs/imx7d-sbc-iot-imx7.dtb
setenv kernel cl-som-imx7-kernel_${kernel_rev}.snap/kernel.img

setenv snap_core core_${core_rev}.snap
setenv snap_kernel cl-som-imx7-kernel_${kernel_rev}.snap

setenv bootargs ${bootargs} console=${console},115200 vmalloc=400M consoleblank=0 rootwait
setenv bootargs ${bootargs} root=/dev/disk/by-label/writable
setenv snappy_cmdline net.ifnames=0 init=/lib/systemd/systemd ro panic=-1 snap_core=${snap_core} snap_kernel=${snap_kernel}
setenv bootargs ${bootargs} ${snappy_cmdline}

${boot_media_init}
load ${boot_media} ${dev} ${fdtaddr} ${fdtfile}
load ${boot_media} ${dev} ${loadaddr} ${kernel}
load ${boot_media} ${dev} ${rdaddr} ${ramimg} && setenv rsize ${filesize}
bootz ${loadaddr} ${rdaddr}:${rsize} ${fdtaddr}
